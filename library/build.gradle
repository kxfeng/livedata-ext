apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'digital.wup.android-maven-publish'
apply plugin: 'com.jfrog.bintray'

ext {
    PUBLISH_GROUP_ID = "com.github.kxfeng"
    PUBLISH_ARTIFACT_ID = 'livedata-ext'
    PUBLISH_VERSION = '1.0.0'
}

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 29
        versionCode 1
        versionName "1.0.0"
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    compileOptions {
        kotlinOptions.freeCompilerArgs += ['-module-name', "$PUBLISH_GROUP_ID.$PUBLISH_ARTIFACT_ID"]
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "androidx.lifecycle:lifecycle-livedata:2.1.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    testImplementation 'junit:junit:4.12'
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    testImplementation "androidx.lifecycle:lifecycle-runtime:2.1.0"
}

bintray {
    user = findProperty('BINTRAY_USER')
    key = findProperty('BINTRAY_KEY')
    pkg {
        repo = 'maven'
        name = "$PUBLISH_ARTIFACT_ID"
        version {
            name = "$PUBLISH_VERSION"
            desc = "$PUBLISH_VERSION release"
            released  = new Date()
            vcsTag = "$PUBLISH_VERSION"
        }
    }
    publications = ['libraryAar']
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier = "sources"
}

publishing {
    publications {
        libraryAar(MavenPublication) {
            from components.android
            groupId = "$PUBLISH_GROUP_ID"
            artifactId = "$PUBLISH_ARTIFACT_ID"
            version = "$PUBLISH_VERSION"
            artifact sourceJar
        }
    }
}