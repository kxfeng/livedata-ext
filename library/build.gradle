apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'digital.wup.android-maven-publish'
apply plugin: 'com.jfrog.bintray'

def _version = "0.9.0"

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 1
        versionName "$_version"
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "androidx.lifecycle:lifecycle-livedata:2.0.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    testImplementation 'junit:junit:4.12'
    testImplementation "androidx.arch.core:core-testing:2.0.0"
    testImplementation "androidx.lifecycle:lifecycle-runtime:2.0.0"
}

bintray {
    user = findProperty('BINTRAY_USER')
    key = findProperty('BINTRAY_KEY')
    pkg {
        repo = 'maven'
        name = 'livedata-ext'
        version {
            name = "$_version"
            desc = "$_version release"
            released  = new Date()
            vcsTag = "$_version"
        }
    }
    publications = ['libraryAar']
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

publishing {
    publications {
        libraryAar(MavenPublication) {
            from components.android
            groupId = 'com.github.kxfeng'
            artifactId = 'livedata-ext'
            version = "$_version"
            artifact sourceJar
        }
    }
}